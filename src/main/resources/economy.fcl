// Block definition (there may be more than one block per file)
// these numbers are placeholders, may way to use functions of some kind
FUNCTION_BLOCK economy

// Define input variables
VAR_INPUT
    publicBalance : REAL;
    govtBalance : REAL;
    og : REAL;
    balanceHighNegative: REAL;
    balanceNeutralNegative: REAL;
    balanceNeutralPositive : REAL;
    balanceHighPositive : REAL;
    spendingHighNegative : REAL;
    spendingNeutralNegative : REAL;
    spendingNeutralPositive : REAL;
    spendingHighPositive : REAL;
    ogHighNegative : REAL;
    ogLowNegative : REAL;
    ogLowPositive : REAL;
    ogHighPositive : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    publicSpending : REAL;
    govtSpending : REAL;
END_VAR

FUZZIFY publicBalance
    TERM debt := (balanceHighNegative, 1.0) (balanceNeutralNegative, 0.0);
    TERM neutral := (balanceHighNegative, 0.0) (balanceNeutralNegative, 1.0) (balanceNeutralPositive, 1.0) (balanceHighPositive, 0.0);
    TERM reserve := (balanceNeutralPositive, 0.0) (balanceHighPositive, 1.0);
END_FUZZIFY

FUZZIFY govtBalance
    TERM debt := (balanceHighNegative, 1.0) (balanceNeutralNegative, 0.0);
    TERM neutral := (balanceHighNegative, 0.0) (balanceNeutralNegative, 1.0) (balanceNeutralPositive, 1.0) (balanceHighPositive, 0.0);
    TERM reserve := (balanceNeutralPositive, 0.0) (balanceHighPositive, 1.0);
END_FUZZIFY

FUZZIFY og
    TERM negative := (ogHighNegative, 1.0) (ogLowNegative, 0.0);
    TERM correct := (ogHighNegative, 0.0) (ogLowNegative, 1.0) (ogLowPositive, 1.0) (ogHighPositive, 0.0);
    TERM positive := (ogLowPositive, 0.0) (ogHighPositive, 1.0);
END_FUZZIFY

DEFUZZIFY publicSpending
    TERM surplus := (spendingHighNegative, 1) (spendingNeutralNegative, 0);
    TERM neutral := (spendingNeutralNegative,0) (0,1) (spendingNeutralPositive,0);
    TERM deficit := (spendingNeutralPositive, 0) (spendingHighPositive, 1);
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY govtSpending
    TERM surplus := (spendingHighNegative, 1) (spendingNeutralNegative, 0);
    TERM neutral := (spendingNeutralNegative,0) (0,1) (spendingNeutralPositive,0);
    TERM deficit := (spendingNeutralPositive, 0) (spendingHighPositive, 1);
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
// Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF govtBalance IS reserve OR
    (og IS negative AND govtBalance IS neutral)
    THEN govtSpending IS deficit;

    RULE 2 : IF og IS correct OR
    (og IS negative AND govtBalance IS debt)
    THEN govtSpending IS neutral;

    RULE 3 : IF govtBalance IS debt OR
    (og IS positive AND govtBalance IS neutral)
    THEN govtSpending IS surplus;
END_RULEBLOCK

RULEBLOCK No2
// Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF publicBalance IS reserve OR
    (og IS negative AND publicBalance IS neutral)
    THEN publicSpending IS deficit;

    RULE 2 : IF og IS correct OR
    (og IS negative AND publicBalance IS debt)
    THEN publicSpending IS neutral;

    RULE 3 : IF publicBalance IS debt OR
    (og IS positive AND publicBalance IS neutral)
    THEN publicSpending IS surplus;
END_RULEBLOCK

END_FUNCTION_BLOCK